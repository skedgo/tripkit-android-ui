apply plugin: 'com.android.library'
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: 'maven-publish'
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion "33.0.0"


    defaultConfig {
        minSdkVersion versions.proMinSdkVersion
        targetSdkVersion versions.targetSdkVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        staging {
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            withJavadocJar()
        }
    }
    namespace 'com.skedgo.tripkit.ui.domain'

}

dependencies {
    implementation libs.kotlin
    implementation libs.rxjava2
    implementation libs.rxrelay2
    implementation libs.jodaTimeAndroid

    implementation libs.dagger
    implementation libs.timber
    kapt libs.daggerCompiler
    kapt libs.value
    compileOnly libs.valueAnnotations

    implementation libs.koptional

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    debugImplementation project(':TripKitAndroid')
    stagingImplementation project(':TripKitAndroid')
    releaseImplementation project(':TripKitAndroid')
}

group = tripkitGroup
version = tripkitVersion

publishing {
    repositories {
        maven {
            url = "file://${localMaven}"
            //url = publishMavenUrl
            credentials {
                username = publishMavenUsername
                password = publishMavenToken
            }
        }
    }

    publications {
        release(MavenPublication) {
            groupId = group
            version = version
            artifactId = 'TripKitAndroidUIDomain'
            afterEvaluate {
                from components.release
            }
            pom {
                licenses {
                    license {
                        name = 'Commercial'
                    }
                }
            }
            pom.withXml {
                asNode().dependencies.'*'
                        .findAll() {
                            (it.scope.text() == 'runtime' || it.scope.text() == 'compile') &&
                                    project.configurations.implementation.allDependencies.find {
                                        dep -> dep.name == it.artifactId.text()
                                    }
                        }.each {
                    it.appendNode("type", "aar")
                }
                asNode().dependencies.'*'
                        .findAll() {
                            it.scope.text() == 'runtime' &&
                                    project.configurations.implementation.allDependencies.find {
                                        dep -> dep.name == it.artifactId.text()
                                    }
                        }.each { it.scope*.value = 'compile'}
            }
        }
    }
}

